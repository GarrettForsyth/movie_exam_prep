apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-allopen'

allOpen {
    annotation 'com.android.example.movieexamprep.OpenClass'
}

android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools
    defaultConfig {
        applicationId "com.example.android.movieexamprep"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName "1.0"
        //testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        testInstrumentationRunner "com.example.android.movieexamprep.util.MovieTestRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }
    dataBinding {
        enabled = true
    }
}

dependencies {

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Support library
    implementation deps.support.app_compat
    implementation deps.support.constraint_layout
    implementation deps.support.recyclerview
    implementation deps.support.design

    // Glide
    implementation deps.glide.runtime
    annotationProcessor deps.glide.compiler

    // architecture components
    implementation deps.lifecycle.runtime
    implementation deps.lifecycle.extensions
    implementation deps.room.runtime
    implementation deps.paging.runtime

    // retrofit
    implementation deps.retrofit.runtime
    implementation deps.retrofit.gson
    implementation deps.retrofit.mock
    implementation deps.okhttp_logging_interceptor

    // Dagger
    implementation deps.dagger.runtime
    implementation deps.dagger.android
    implementation deps.dagger.android_support

    // Customized movie views
    implementation project(':movieviews')

    kapt deps.lifecycle.compiler
    kapt deps.room.compiler
    kapt deps.dagger.compiler
    kapt deps.dagger.android_support_compiler


    // Unit test dependencies
    testImplementation deps.junit
    testImplementation deps.mock_web_server
    testImplementation deps.arch_core.testing
    testImplementation deps.mockito.core

    // Instrumentation test dependencies
    androidTestImplementation deps.atsl.runner
    androidTestImplementation deps.atsl.rules

    androidTestImplementation deps.support.app_compat
    androidTestImplementation deps.support.recyclerview
    androidTestImplementation deps.support.constraint_layout
    androidTestImplementation deps.support.design

    androidTestImplementation deps.espresso.core
    androidTestImplementation deps.espresso.contrib

    androidTestImplementation deps.arch_core.testing
    androidTestImplementation deps.mockito.core
    androidTestImplementation deps.mockito.android

}

